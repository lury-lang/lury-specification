<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
  <title>6. 複合文 - Lury Specification 0.1</title>
  <script src="../../common/loader.js"></script>
  <script type="text/javascript">loader_load('../../')</script>
</head>
<body>
  <div class="container">
    <div class="row">
      <div class="col-main markdown">

## 6. 複合文

```
compound\_statement : if\_statement
                   | while\_statement
                   | function\_definition
                   | class\_definition

suite              : statement\_list NewLine
                   | NewLine Indent statements Dedent

statements         : statement
                   | statement statements

statement          : statement\_list NewLine
                   | compound\_statement

statement\_list     : simple\_statement 
                   | simple\_statement simple\_statements

simple\_statements  : ';'
                   | ';' simple\_statement simple\_statements
```

_複合文 (compound statement)_ は 1 行以上の論理行にまたがる文のことです。複合文は 2 種類の書き方があり、セミコロン `;` を使った記述と、インデントを使った記述があります。

セミコロンによる記述は [単純文](simple-statement.html) をセミコロンで繋ぐことで、複数個記述することができます。行末のセミコロンは任意です。

インデントによる記述は [オフサイドルール](lexical.html#indent) によってスコープが定まります。スコープ内に定義された変数の寿命と有報範囲は同じスコープ内でのみ有効です。

全ての複合文はその種類に関係なく _入れ子 (nest)_ にすることができます。

<section id="if">
### 6.1. if 文

```
if\_statement    : 'if' expression ':' suite
                | 'if' expression ':' suite elif\_statements

elif\_statements : 'elif' expression ':' suite
                | 'elif' expression ':' suite elif\_statements
                | else\_statement

else\_statement  : 'else' ':' suite
```

_if 文_ は条件分岐を行うための文です。被評価式 expression を条件とし、`true` であれば if 文内の suite で示される処理行い、`false` でありかつ elif もしくは else ブロックが存在するならばそのブロックについても同様に条件分岐を行います。

条件に合致しなかったブロックの suite は処理されません。

elif ブロックはいくつでも記述が可能です。一方、else ブロックは多くとも 1 つのみ記述が可能で、それは必ず if 文の最後に記述しなければなりません。

</section>

<section id="while">
### 6.2. while 文

```
while\_statement : 'while' expression ':' suite
                | 'while' expression ':' suite else\_statements
```

_while 文_ は反復処理を行うための文です。被評価式 expression を条件とし、`true` である限りブロック内の処理を反復処理します。最初に while 文に遭遇したとき、expression の値が `false` である場合はブロック内を一度も実行しません。

while 文の else ブロックは _expression が `false` である場合に while 文を脱出したとき_ に限り実行されます。[break 文](simple-statement.html#break) や [return 文](simple-statement.html#return) による脱出時は else ブロックは実行されません。一方で前述したような、最初から expression の値が `false` であった場合は else ブロックは実行されます。

</section>

<section id="function">
### 6.3. 関数定義

```
function\_definition : 'def' function\_name '(' parameter\_list ')' ':' suite
                    | 'def' function\_name ':' suite

function\_name       : identifier\_all
                    | function\_name '.' identifier\_all

parameter\_list      : parameter
                    | parameter\_list ',' parameter

parameter           : identifier\_all
```

関数定義は関数を定義するための文です。キーワード `def` から始まり、括弧で囲われたパラメータ引数（仮引数）と実行内容を記述します。このうち、括弧と引数は省略が可能です。

関数定義では関数は実行されません。定義ののち、関数呼び出しによって実行が可能です。

</section>

<section id="class">
### 6.4. クラス定義

```
class\_definition  : 'class' class\_name '(' inheritance\_list ')' ':' suite
                  | 'class' class\_name ':' suite

class\_name        : identifier\_all

inheritance\_list  : object\_name
                  | inheritance\_list ',' object\_name

object\_name       : identifier\_all
                  | object\_name '.' identifier\_all
```

クラス定義はクラスを定義するための文です。キーワード `class` から始まり、括弧で他のクラスの名前を指定してそのクラスを継承することができます。継承をしない場合は括弧を省略できますが、暗黙的に `Object` クラスを継承します。

</section>

<section id="cite">
### 参考文献

- [8. 複合文 (compound statement) — Python 3.4.3 ドキュメント](http://docs.python.jp/3.4/reference/compound_stmts.html)

</section>

      </div>
    </div>
  </div>
</body>
</html>
