<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
  <title>8. 完全な文法 - Lury Specification 0.1</title>
  <script src="../../common/loader.js"></script>
  <script type="text/javascript">loader_load('../../')</script>
</head>
<body>
  <div class="container">
    <div class="row">
      <div class="col-main markdown">

## 8. 完全な文法

以下はパーサジェネレータに入力される Lury の完全な文法です。(BNF 記法)

```bnf
program
    :
    | program_lines

program_lines
    : program_line
    | program_lines program_line

program_line
    : statement
    | NewLine


statement
    : statement_list NewLine
    | compound_statement

statement_list
    : simple_statement
    | simple_statement simple_statements

simple_statements
    : ';'
    | ';' simple_statement
    | ';' simple_statement simple_statements

compound_statement
    : if_statement
    | while_statement
    | function_definition
    | class_definition
    | unittest_statement

suite
    : statement_list NewLine
    | NewLine Indent statements Dedent

statements
    : statement
    | statement statements


if_statement
    : KeywordIf expression ':' suite
    | KeywordIf expression ':' suite elif_statements

elif_statements
    : KeywordElif expression ':' suite
    | KeywordElif expression ':' suite elif_statements
    | else_statement

else_statement
    : KeywordElse ':' suite

while_statement
    : KeywordWhile expression ':' suite
    | KeywordWhile expression ':' suite else_statement

function_definition
    : KeywordDef function_name '(' parameter_list ')' ':' suite
    | KeywordDef function_name ':' suite

function_name
    : identifier_all
    | function_name '.' identifier_all

parameter_list
    : parameter
    | parameter_list ',' parameter

parameter
    : identifier_all

class_definition
    : KeywordClass class_name '(' inheritance_list ')' ':' suite
    | KeywordClass class_name ':' suite

class_name
    : identifier_all

inheritance_list
    : object_name
    | inheritance_list ',' object_name

object_name
    : identifier_all
    | object_name '.' identifier_all

unittest_statement
    : 'unittest' ':' suite


simple_statement
    : pass_statement
    | expression_statement
    | return_statement
    | break_statement
    | continue_statement
    | import_statement

pass_statement
    : KeywordPass

expression_statement
    : expression

return_statement
    : KeywordReturn
    | KeywordReturn expression

break_statement
    : KeywordBreak

continue_statement
    : KeywordContinue

import_statement
    : KeywordImport import_names
    | KeywordPublic KeywordImport import_names

import_names
    : import_name
    | import_names ',' import_name

import_name
    : module_name

module_name
    : identifier_all
    | module_name '.' identifier_all

identifier_all
    : Identifier
    | contextual_keyword

contextual_keyword
    : IdentifierGet
    | IdentifierSet
    | IdentifierFile
    | IdentifierLine
    | IdentifierExit
    | IdentifierSuccess
    | IdentifierFailure


expression
    : comma_expression

comma_expression
    : assignment_expression
    | assignment_expression ',' comma_expression

assignment_expression
    : conditional_expression
    | conditional_expression '=' assignment_expression
    | conditional_expression AssignmentAdd assignment_expression
    | conditional_expression AssignmentSub assignment_expression
    | conditional_expression AssignmentMultiply assignment_expression
    | conditional_expression AssignmentDivide assignment_expression
    | conditional_expression AssignmentIntDivide assignment_expression
    | conditional_expression AssignmentPower assignment_expression
    | conditional_expression AssignmentModulo assignment_expression
    | conditional_expression AssignmentAnd assignment_expression
    | conditional_expression AssignmentOr assignment_expression
    | conditional_expression AssignmentXor assignment_expression
    | conditional_expression AssignmentConcat assignment_expression
    | conditional_expression AssignmentLeftShift assignment_expression
    | conditional_expression AssignmentRightShift assignment_expression

conditional_expression
    : bool_or_expression
    | bool_or_expression '?' bool_or_expression ':' bool_or_expression
    | bool_or_expression KeywordIf bool_or_expression KeywordElse bool_or_expression

bool_or_expression
    : bool_and_expression
    | bool_or_expression KeywordOr bool_and_expression
    | bool_or_expression OrShort bool_and_expression

bool_and_expression
    : bool_not_expression
    | bool_and_expression KeywordAnd bool_not_expression
    | bool_and_expression AndShort bool_not_expression

bool_not_expression
    : comparison_expression
    | KeywordNot bool_not_expression
    | '!' bool_not_expression

comparison_expression
    : or_expression
    | or_expression comparison_operator or_expression

comparison_operator
    : '&lt;'
    | '&gt;'
    | Equal
    | LessThan
    | MoreThan
    | NotEqual
    | KeywordIs
    | IsNot
    | KeywordIs KeywordNot

or_expression
    : xor_expression
    | or_expression '\|' xor_expression

xor_expression
    : and_expression
    | xor_expression '^' and_expression

and_expression
    : shift_expression
    | and_expression '&amp;' shift_expression

shift_expression
    : addition_expression
    | shift_expression LeftShift addition_expression
    | shift_expression RightShift addition_expression

addition_expression
    : multiplication_expression
    | addition_expression '+' multiplication_expression
    | addition_expression '-' multiplication_expression
    | addition_expression '~' multiplication_expression

multiplication_expression
    : unary_expression
    | multiplication_expression '\*' unary_expression
    | multiplication_expression IntDivide unary_expression
    | multiplication_expression '/' unary_expression
    | multiplication_expression '%' unary_expression

unary_expression
    : power_expression
    | Increment unary_expression
    | Decrement unary_expression
    | '-' unary_expression
    | '+' unary_expression
    | '~' unary_expression

power_expression
    : callref_expression
    | power_expression Power callref_expression

callref_expression
    : postfix_expression
    | KeywordRef postfix_expression

postfix_expression
    : primary
    | postfix_expression '.' identifier_all
    | postfix_expression Increment
    | postfix_expression Decrement
    | postfix_expression '(' ')'
    | postfix_expression '(' argument_list ')'

argument_list
    : argument
    | argument ',' argument_list

argument
    : assignment_expression
    | named_argument

named_argument
    : identifier_all ':' expression

primary
    : identifier_all
    | literal
    | KeywordTrue
    | KeywordFalse
    | KeywordNil
    | KeywordThis
    | KeywordSuper
    | '(' expression ')'

literal
    : StringLiteral
    | EmbedStringLiteral
    | WysiwygStringLiteral
    | ImaginaryNumber
    | FloatNumber
    | Integer
```

      </div>
    </div>
  </div>
  <script type="text/javascript">
    $(function() {
      build_header_nav([]);
    });
  </script>
</body>
</html>
