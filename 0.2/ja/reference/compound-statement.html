<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
  <title>6. 複合文 - Lury Specification 0.1</title>
  <script src="../../common/loader.js"></script>
  <script type="text/javascript">loader_load('../../')</script>
</head>
<body>
  <div class="container">
    <div class="row">
      <div class="col-main markdown">

## 6. 複合文

```bnf
compound_statement : if_statement
                   | while_statement
                   | function_definition
                   | class_definition
                   | unittest_statement

suite              : statement_list NewLine
                   | NewLine Indent statements Dedent

statements         : statement
                   | statement statements

statement          : statement_list NewLine
                   | compound_statement

statement_list     : simple_statement
                   | simple_statement simple_statements

simple_statements  : ';'
                   | ';' simple_statement
                   | ';' simple_statement simple_statements
```

_複合文 (compound statement)_ は 1 行以上の論理行にまたがる文のことです。複合文は 2 種類の書き方があり、セミコロン `;` を使った記述と、インデントを使った記述があります。

セミコロンによる記述は [単純文](simple-statement.html) をセミコロンで繋ぐことで、複数個記述することができます。行末のセミコロンは任意です。

インデントによる記述は [オフサイドルール](lexical.html#indent) によってスコープが定まります。スコープ内に定義された変数の寿命と有報範囲は同じスコープ内でのみ有効です。

unittest 文を除き、複合文は _入れ子 (nest)_ にすることができます。


### 6.1. if 文

```bnf
if_statement    : 'if' expression ':' suite
                | 'if' expression ':' suite elif_statements

elif_statements : 'elif' expression ':' suite
                | 'elif' expression ':' suite elif_statements
                | else_statement

else_statement  : 'else' ':' suite
```

_if 文_ は条件分岐を行うための文です。被評価式 expression を条件とし、`true` であれば if 文内の suite で示される処理行い、`false` でありかつ elif もしくは else ブロックが存在するならばそのブロックについても同様に条件分岐を行います。

条件に合致しなかったブロックの suite は処理されません。

elif ブロックはいくつでも記述が可能です。一方、else ブロックは多くとも 1 つのみ記述が可能で、それは必ず if 文の最後に記述しなければなりません。


### 6.2. while 文

```bnf
while_statement : 'while' expression ':' suite
                | 'while' expression ':' suite else_statements
```

_while 文_ は反復処理を行うための文です。被評価式 expression を条件とし、`true` である限りブロック内の処理を反復処理します。最初に while 文に遭遇したとき、expression の値が `false` である場合はブロック内を一度も実行しません。

while 文の else ブロックは _expression が `false` である場合に while 文を脱出したとき_ に限り実行されます。[break 文](simple-statement.html#break) や [return 文](simple-statement.html#return) による脱出時は else ブロックは実行されません。一方で前述したような、最初から expression の値が `false` であった場合は else ブロックは実行されます。


### 6.3. 関数定義

```bnf
function_definition : 'def' function_name '(' parameter_list ')' ':' suite
                    | 'def' function_name ':' suite

function_name       : identifier_all
                    | function_name '.' identifier_all

parameter_list      : parameter
                    | parameter_list ',' parameter

parameter           : identifier_all
```

関数定義は関数を定義するための文です。キーワード `def` から始まり、括弧で囲われたパラメータ引数（仮引数）と実行内容を記述します。このうち、括弧と引数は省略が可能です。

関数定義では関数は実行されません。定義ののち、関数呼び出しによって実行が可能です。


### 6.4. クラス定義

```bnf
class_definition  : 'class' class_name '(' inheritance_list ')' ':' suite
                  | 'class' class_name ':' suite

class_name        : identifier_all

inheritance_list  : object_name
                  | inheritance_list ',' object_name

object_name       : identifier_all
                  | object_name '.' identifier_all
```

クラス定義はクラスを定義するための文です。キーワード `class` から始まり、括弧で他のクラスの名前を指定してそのクラスを継承することができます。継承をしない場合は括弧を省略できますが、暗黙的に `Object` クラスを継承します。

これは、次のようなクラス定義は

```lury
class Foo:
  pass

class Bar(Foo):
  pass
```

実際には以下のように、クラス `Foo` はクラス `Object` を継承するよう暗黙的に宣言されます。

```lury
class Foo(Object):
  pass

class Bar(Foo):
  pass
```


### 6.5. unittest 文

```bnf
unittest_statement : 'unittest' ':' suite
```

_unittest 文_ は単体テストを行うための文です。suite に単体テスト時に行われる処理内容を記述します。

unittest 文は実行環境で _明確に_ 単体テストを行うことを指定されない限り、決して実行されることはありません。また、unittest 文の実行順序は保証されず、実行の有無も保証されません。プログラムの記述者は unittest 文の実行や実行順序に依存したプログラムを記述してはいけません。

unittest 文をユーザコードから呼び出すことはできません。

unittest 文は unittest 文を直接的にも間接的にも入れ子にすることはできません。unittest 文を記述できる場所はクラス定義内のみで、任意の回数記述しても構いません。


### 参考文献

- [8. 複合文 (compound statement) — Python 3.4.3 ドキュメント](http://docs.python.jp/3.4/reference/compound_stmts.html)


      </div>
    </div>
  </div>
  <script type="text/javascript">
    $(function() {
      build_header_nav([
        {id: '6-1-if-', name: 'if'},
        {id: '6-2-while-', name: 'while'},
        {id: '6-3-', name: 'function'},
        {id: '6-4-', name: 'class'},
        {id: '6-5-unittest-', name: 'unittest'}
      ]);
    });
  </script>
</body>
</html>
